FIND_PACKAGE(PkgConfig)
if (XWINDOWS)
    PKG_CHECK_MODULES(PC_GTKGLEXT QUIET gtkglext-x11-1.0)
elseif (WIN32)
    PKG_CHECK_MODULES(PC_GTKGLEXT QUIET gtkglext-win32-1.0)
else ()
    PKG_CHECK_MODULES(PC_GTKGLEXT QUIET gtkglext-quartz-1.0)
endif ()

FIND_LIBRARY(GTKGLEXT_LIBRARIES
    NAMES gtkglext-1.0 gtkglext-x11-1.0 gtkglext-win32-1.0 gtkglext-quartz-1.0
    HINTS ${PC_GTKGLEXT_LIBDIR}
          ${PC_GTKGLEXT_LIBRARY_DIRS}
)

FIND_LIBRARY(GDKGLEXT_LIBRARIES
    NAMES gdkglext-1.0 gdkglext-x11-1.0 gdkglext-win32-1.0 gdkglext-quartz-1.0
    HINTS ${PC_GTKGLEXT_LIBDIR}
          ${PC_GTKGLEXT_LIBRARY_DIRS}
)

# Files in gtkglext's main include path may include gdkglext-config.h, which,
# for some odd reason, is normally in $LIBDIR/gtkglext-1.0/include.
GET_FILENAME_COMPONENT(_GTKGLEXT_LIBRARY_DIR ${GTKGLEXT_LIBRARIES} PATH)
FIND_PATH(GTKGLEXTCONFIG_INCLUDE_DIR
    NAMES gdkglext-config.h
    HINTS ${PC_LIBDIR} ${PC_LIBRARY_DIRS} ${_GTKGLEXT_LIBRARY_DIR}
    PATH_SUFFIXES gtkglext-1.0/include
)

FIND_PATH(GTKGLEXT_INCLUDE_DIR
    NAMES gtkgl.h
    HINTS ${PC_GTKGLEXT_INCLUDEDIR}
          ${PC_GTKGLEXT_INCLUDE_DIRS}
    PATH_SUFFIXES gtkglext-1.0/gtk
)

FIND_PATH(GDKGLEXT_INCLUDE_DIR
    NAMES gdkgl.h
    HINTS ${PC_GTKGLEXT_INCLUDEDIR}
          ${PC_GTKGLEXT_INCLUDE_DIRS}
    PATH_SUFFIXES gtkglext-1.0/gdk
)

SET(GTKGLEXT_INCLUDE_DIRS ${GTKGLEXT_INCLUDE_DIR}/.. ${GDKGLEXT_INCLUDE_DIR}/.. ${GTKGLEXTCONFIG_INCLUDE_DIR})

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(GTKGLEXT REQUIRED_VARS GTKGLEXT_INCLUDE_DIRS GTKGLEXT_LIBRARIES GDKGLEXT_LIBRARIES)
